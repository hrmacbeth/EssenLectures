/-  Author:  S√©bastien Gou√´zel   sebastien.gouezel@univ-rennes1.fr
    License: BSD -/
import GromovHyperbolicity.GromovHyperbolic
import Mathlib.Tactic.Peel
import Mathlib.Topology.MetricSpace.Thickening

/-! # Quasiconvexity -/

open Metric Set

variable {M : Type*} [MetricSpace M]

/-- In a Gromov-hyperbolic setting, convexity is not a well-defined notion as everything should
be coarse. The good replacement is quasi-convexity: A set `x` is `C`-quasi-convex if any pair of
points in `x` can be joined by a geodesic that remains within distance `C` of `x`. One could also
require this for all geodesics, up to changing `C`, as two geodesics between the same endpoints
remain within uniformly bounded distance. We use the first definition to ensure that a geodesic is
0-quasi-convex. -/
structure quasiconvex (C : ‚Ñù) (X : Set M) : Prop :=
  (C_nonneg : 0 ‚â§ C)
  (exists_nearby_geodesic {x y : M} (_ : x ‚àà X) (_ : y ‚àà X) :
    ‚àÉ G, geodesic_segment_between G x y ‚àß (‚àÄ z ‚àà G, infDist z X ‚â§ C))

variable {C D : ‚Ñù} {X G : Set M}

lemma quasiconvex_of_geodesic {G : Set M} (hG : geodesic_segment G) : quasiconvex 0 G where
  C_nonneg := by simp
  exists_nearby_geodesic {x y} (hx hy) := by
    obtain ‚ü®H, hHG, hHxy‚ü© := geodesic_subsegment_exists hG hx hy
    refine ‚ü®H, hHxy, ?_‚ü©
    intro _ _
    rw [infDist_zero_of_mem]
    aesop

lemma quasiconvex_empty (hC : C ‚â• 0) : quasiconvex C (‚àÖ : Set M) where
  C_nonneg := by aesop
  exists_nearby_geodesic := by aesop

lemma quasiconvex_mono (hCD : C ‚â§ D) (hC : quasiconvex C G) : quasiconvex D G where
  C_nonneg := by linarith [hC.C_nonneg]
  exists_nearby_geodesic {x y} (hx hy) := by
    peel hC.exists_nearby_geodesic hx hy
    linarith

variable [Gromov_hyperbolic_space M] [GeodesicSpace M]

local notation "Œ¥" => Gromov_hyperbolic_space.deltaG M

/-- The `r`-neighborhood of a quasi-convex set is still quasi-convex in a hyperbolic space,
for a constant that does not depend on `r`. -/
lemma quasiconvex_thickening [Inhabited M] (h : quasiconvex C X) {r : ‚Ñù} (hr : r ‚â• 0) :
    quasiconvex (C + 8 * Œ¥) (cthickening r X) where
  C_nonneg := by
    have := h.C_nonneg
    have := delta_nonneg M
    positivity
  exists_nearby_geodesic {y z} (hy hz) := by
    refine ‚ü®{y‚Äíz}, sorry, ?_‚ü©
    sorry
-- proof (rule quasiconvexI)
--   show "C + 8 * Œ¥ ‚â• 0" using quasiconvexC[OF assms(1)] by simp
-- next
--   fix y z assume *: "y‚àà(\<Union>x ‚àà X. cball x r)" "z‚àà(\<Union>x ‚àà X. cball x r)"
--   have A: "infDist w (\<Union>x ‚àà X. cball x r) ‚â§ C + 8 * deltaG TYPE('a)" if "w‚àà{y--z}" for w
--   proof -
--     obtain py where py: "py‚ààX" "y‚ààcball py r"
--       using * by auto
--     obtain pz where pz: "pz‚ààX" "z‚ààcball pz r"
--       using * by auto
--     obtain G where G: "geodesic_segment_between G py pz" "(‚àÄ p ‚àà G. infDist p X ‚â§ C)"
--       using quasiconvexD[OF assms(1) \<open>py‚ààX\<close> \<open>pz‚ààX\<close>] by auto
--     have A: "infDist w ({y--py} \<union> G \<union> {pz--z}) ‚â§ 8 * Œ¥"
--       by (rule thin_quadrilaterals[OF _ G(1) _ _ \<open>w‚àà{y--z}\<close>, where ?x = y and ?t = z], auto)
--     have "‚àÉ u‚àà{y--py} \<union> G \<union> {pz--z}. infDist w ({y--py} \<union> G \<union> {pz--z}) = dist w u"
--       apply (rule infDist_proper_attained, auto intro!: proper_Un simp add: geodesic_segment_topology(7))
--       by (meson G(1) geodesic_segmentI geodesic_segment_topology(7))
--     then obtain u where u: "u‚àà{y--py} \<union> G \<union> {pz--z}" "infDist w ({y--py} \<union> G \<union> {pz--z}) = dist w u"
--       by auto
--     then consider "u‚àà{y--py}" | "u‚ààG" | "u‚àà{pz--z}" by auto
--     then have "infDist u (\<Union>x ‚àà X. cball x r) ‚â§ C"
--     proof (cases)
--       case 1
--       then have "dist py u ‚â§ dist py y"
--         using geodesic_segment_dist_le local.some_geodesic_is_geodesic_segment(1) some_geodesic_commute some_geodesic_endpoints(1) by blast
--       also have "... ‚â§ r"
--         using py(2) by auto
--       finally have "u‚ààcball py r"
--         by auto
--       then have "u‚àà(\<Union>x ‚àà X. cball x r)"
--         using py(1) by auto
--       then have "infDist u (\<Union>x ‚àà X. cball x r) = 0"
--         by auto
--       then show ?thesis
--         using quasiconvexC[OF assms(1)] by auto
--     next
--       case 3
--       then have "dist pz u ‚â§ dist pz z"
--         using geodesic_segment_dist_le local.some_geodesic_is_geodesic_segment(1) some_geodesic_commute some_geodesic_endpoints(1) by blast
--       also have "... ‚â§ r"
--         using pz(2) by auto
--       finally have "u‚ààcball pz r"
--         by auto
--       then have "u‚àà(\<Union>x ‚àà X. cball x r)"
--         using pz(1) by auto
--       then have "infDist u (\<Union>x ‚àà X. cball x r) = 0"
--         by auto
--       then show ?thesis
--         using quasiconvexC[OF assms(1)] by auto
--     next
--       case 2
--       have "infDist u (\<Union>x ‚àà X. cball x r) ‚â§ infDist u X"
--         apply (rule infDist_mono) using assms(2) py(1) by auto
--       then show ?thesis using 2 G(2) by auto
--     qed
--     moreover have "infDist w (\<Union>x ‚àà X. cball x r) ‚â§ infDist u (\<Union>x ‚àà X. cball x r) + dist w u"
--       by (intro mono_intros)
--     ultimately show ?thesis
--       using A u(2) by auto
--   qed
--   show "‚àÉ G. geodesic_segment_between G y z ‚àß (‚àÄ w ‚àà G. infDist w (\<Union>x ‚àà X. cball x r) ‚â§ C + 8 * deltaG TYPE('a))"
--     apply (rule exI[of _ "{y--z}"]) using A by auto
-- qed

/-- If `x` has a projection `p` on a quasi-convex set `G`, then all segments from a point in `G`
to `x` go close to `p`, i.e., the triangular inequality $d(x,y) ‚â§ d(x,p) + d(p,y)$ is essentially
an equality, up to an additive constant. -/
lemma dist_along_quasiconvex (hCG : quasiconvex C G) {p x : M} (hp : p ‚àà proj_set x G) {y : M} (hy : y ‚àà G) :
    dist x p + dist p y ‚â§ dist x y + 4 * Œ¥ + 2 * C := by
  have : p ‚àà G := hp.1
  obtain ‚ü®H, hH‚ÇÅ, hH‚ÇÇ‚ü© : ‚àÉ H, geodesic_segment_between H p y ‚àß ‚àÄ q ‚àà H, infDist q G ‚â§ C :=
    hCG.exists_nearby_geodesic this hy
  obtain ‚ü®m, hm‚ÇÅ, hm‚ÇÇ‚ü© : ‚àÉ m ‚àà H, infDist x H = dist x m := by
    apply IsCompact.exists_infDist_eq_dist
    ¬∑ refine (geodesic_segment_topology ?_).1
      exact ‚ü®_, _, hH‚ÇÅ‚ü©
    ¬∑ exact (geodesic_segment_endpoints hH‚ÇÅ).2.2
  have I : dist x m ‚â§ Gromov_product_at x p y + 2 * Œ¥ := by
    rw [‚Üê hm‚ÇÇ]
    apply infDist_triangle_side x hH‚ÇÅ
  have : ‚àÄ e > 0, dist x p - dist x m - C ‚â§ e := by
    intro e he
    obtain ‚ü®r, hrG, hrm‚ü© : ‚àÉ r ‚àà G, dist m r < infDist m G + e := by
      rw [‚Üê infDist_lt_iff]
      ¬∑ linarith
      ¬∑ exact ‚ü®_, hy‚ü©
    have : infDist m G ‚â§ C := hH‚ÇÇ _ hm‚ÇÅ
    have :=
    calc dist x p ‚â§ dist x r := proj_set_dist_le hrG hp
      _ ‚â§ dist x m + dist m r := dist_triangle ..
--     finally show ?thesis using * by (auto simp add: metric_space_class.dist_commute)
    linarith
  have : dist x p - dist x m - C ‚â§ 0 := le_of_forall_le_of_dense this
  rw [Gromov_product_at] at I
  linarith
--   then show ?thesis
--     using I unfolding Gromov_product_at_def by (auto simp add: algebra_simps divide_simps)
-- qed

/-- The next lemma is~\<^cite> Proposition 10.2.1 in "coornaert_delzant_papadopoulos" with better
constants. It states that the distance between the projections
on a quasi-convex set is controlled by the distance of the original points, with a gain given by the
distances of the points to the set. -/
-- **Lemma 2.4**
lemma proj_along_quasiconvex_contraction (h : quasiconvex C G) {px x : M} (hx : px ‚àà proj_set x G)
    {py y : M} (hy : py ‚àà proj_set y G) :
    dist px py ‚â§ max (5 * Œ¥ + 2 * C) (dist x y - dist px x - dist py y + 10 * Œ¥ + 4 * C) := by
  have := dist_along_quasiconvex h hx <| hy.1
  have := dist_along_quasiconvex h hy <| hx.1
  have := Gromov_hyperbolic_space.hyperb_quad_ineq x py y px
  simp only [dist_comm] at *
  obtain _ | _ := max_cases (5 * Œ¥ + 2 * C) (dist x y - dist px x - dist py y + 10 * Œ¥ + 4 * C) <;>
  obtain _ | _ := max_cases (dist x y + dist px py) (dist px x + dist py y) <;>
  linarith

/-- The projection on a quasi-convex set is 1-Lipschitz up to an additive error. -/
lemma proj_along_quasiconvex_contraction' (h : quasiconvex C G) {px x : M} (hx : px ‚àà proj_set x G)
    {py y : M} (hy : py ‚àà proj_set y G) :
    dist px py ‚â§ dist x y + 4 * Œ¥ + 2 * C := by
  have := dist_along_quasiconvex h hx <| hy.1
  have := dist_along_quasiconvex h hy <| hx.1
  have := dist_triangle x y py
  have := dist_triangle y x px
  simp only [dist_comm] at *
  linarith

/-- We can in particular specialize the previous statements to geodesics, which are
0-quasi-convex. -/
lemma dist_along_geodesic (h : geodesic_segment G) {p x : M} (hx : p ‚àà proj_set x G) {y : M} (hy : y ‚àà G) :
    dist x p + dist p y ‚â§ dist x y + 4 * Œ¥ := by
  have H := dist_along_quasiconvex (quasiconvex_of_geodesic h) hx hy
  ring_nf at H ‚ä¢
  exact H

lemma proj_along_geodesic_contraction (h : geodesic_segment G) {px x : M} (hx : px ‚àà proj_set x G)
    {py y : M} (hy : py ‚àà proj_set y G) :
    dist px py ‚â§ max (5 * Œ¥) (dist x y - dist px x - dist py y + 10 * Œ¥) := by
  have H := proj_along_quasiconvex_contraction (quasiconvex_of_geodesic h) hx hy
  ring_nf at H ‚ä¢
  exact H

lemma proj_along_geodesic_contraction' (h : geodesic_segment G) {px x : M} (hx : px ‚àà proj_set x G)
    {py y : M} (hy : py ‚àà proj_set y G) :
    dist px py ‚â§ dist x y + 4 * Œ¥ := by
  have H := proj_along_quasiconvex_contraction' (quasiconvex_of_geodesic h) hx hy
  ring_nf at H ‚ä¢
  exact H

open Topology

/-- If one projects a continuous curve on a quasi-convex set, the image does not have to be
connected (the projection is discontinuous), but since the projections of nearby points are within
uniformly bounded distance one can find in the projection a point with almost prescribed distance
to the starting point, say. For further applications, we also pick the first such point, i.e.,
all the previous points are also close to the starting point. -/
-- **Lemma 2.2** in article.
-- not sure whether inequalities are sharp or non-sharp
lemma quasi_convex_projection_small_gaps {f p : ‚Ñù ‚Üí M} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    (hab : a ‚â§ b)
    (h : quasiconvex C G)
    (hfG : ‚àÄ t ‚àà Icc a b, p t ‚àà proj_set (f t) G)
    {delta : ‚Ñù} (hdelta : delta > Œ¥)
    {d : ‚Ñù} (hd : d ‚àà Icc (4 * delta + 2 * C) (dist (p a) (p b))) :
    ‚àÉ t ‚àà Icc a b, dist (p a) (p t) ‚àà Icc (d - 4 * delta - 2 * C) d
                    ‚àß ‚àÄ s ‚àà Icc a t, dist (p a) (p s) ‚â§ d := by
  have : Inhabited M := ‚ü®f 0‚ü©
  have : 0 ‚â§ Œ¥ := delta_nonneg M
  have : 0 ‚â§ C := h.C_nonneg
  have hd0 : 0 ‚â§ d := by linarith [hd.1]

/- The idea is to define the desired point as the last point `u` for which there is a projection
at distance at most `d` of the starting point. Then the projection can not be much closer to
the starting point, or one could point another such point further away by almost continuity, giving
a contradiction. The technical implementation requires some care, as the "last point" may not
satisfy the property, for lack of continuity. If it does, then fine. Otherwise, one should go just
a little bit to its left to find the desired point. -/
  obtain ‚ü®u, ‚ü®hau, hub‚ü©, A, H3‚ü© : ‚àÉ u ‚àà Icc a b, (‚àÄ s ‚àà Ico a u, dist (p a) (p s) ‚â§ d)
      ‚àß (u < b ‚Üí ‚àÉ·∂† s in ùìù[‚â•] u, ¬¨ dist (p a) (p s) ‚â§ d) :=
    method_of_continuity hab (P := fun s ‚Ü¶ dist (p a) (p s) ‚â§ d) (by simpa)

  have hf2 : ContinuousWithinAt f (Icc a b) u := hf.continuousWithinAt ‚ü®hau, hub‚ü©
  have hdeltaŒ¥ : 0 < delta - Œ¥ := by linarith
  have H1 : ‚àÄ·∂† s in ùìù[Icc a b] u, dist (f s) (f u) < delta - Œ¥ :=
    hf2.tendsto <| ball_mem_nhds (f u) hdeltaŒ¥

  by_cases hdp : dist (p a) (p u) > d
/- First, consider the case where `u` does not satisfy the defining property. Then the
desired point `t` is taken slightly to its left. -/
  ¬∑ obtain ‚ü®t, ‚ü®hta, htu‚ü©, htue0‚ü© : ‚àÉ t ‚àà Ico a u, dist (f t) (f u) < delta - Œ¥ := by
      have hau : a < u := lt_of_le_of_ne hau <| by rintro rfl; linarith [dist_self (p a)]
      have : (ùìù[<] u).NeBot := nhdsWithin_Iio_self_neBot _
      have H2 : ‚àÄ·∂† s in ùìù[<] u, s ‚àà Ico a u := Ico_mem_nhdsWithin_Iio' hau
      have : Ico a u ‚äÜ Icc a b := Ico_subset_Icc_self.trans <| Icc_subset_Icc_right hub
      have := H1.filter_mono (nhdsWithin_mono _ this)
      rw [nhdsWithin_Ico_eq_nhdsWithin_Iio hau] at this
      exact (H2.and this).exists

    have htab : t ‚àà Icc a b := ‚ü®hta, htu.le.trans hub‚ü©
    refine ‚ü®t, htab, ?_‚ü©

    have H1 : ‚àÄ s ‚àà Icc a t, dist (p a) (p s) ‚â§ d := by
      intro s hs
      exact A s ‚ü®hs.1, trans hs.2 htu‚ü©

    refine ‚ü®‚ü®?_, H1 _ ?_‚ü©, H1‚ü©
    ¬∑ have : dist (p t) (p u) ‚â§ dist (f t) (f u) + 4 * Œ¥ + 2 * C :=
            proj_along_quasiconvex_contraction' h (hfG t htab) (hfG u ‚ü®hau, hub‚ü©)
      have : dist (p a) (p u)  ‚â§ dist (p a) (p t) + dist (p t) (p u) := dist_triangle ..
      linarith
    ¬∑ rwa [right_mem_Icc]
/- Next, consider the case where `u` satisfies the defining property. Then we will take `t = u`.
The only nontrivial point to check is that the distance of `f u` to the starting point is not
too small. For this, we need to separate the case where `u = b` (in which case one argues directly)
and the case where `u < b`, where one can use a point slightly to the right of `u` which has a
projection at distance > `d` of the starting point, and use almost continuity. -/
  ¬∑ push_neg at hdp H3
    have B : ‚àÄ s ‚àà Icc a u, dist (p a) (p s) ‚â§ d := by
      intro s hs
      obtain rfl | hsu := eq_or_lt_of_le hs.2
      ¬∑ exact hdp
      ¬∑ exact A _ ‚ü®hs.1, hsu‚ü©
    have huau : u ‚àà Icc a u := by rwa [right_mem_Icc]
    refine ‚ü®u, ‚ü®hau, hub‚ü©, ‚ü®?_, B _ huau‚ü©, B‚ü©
    obtain rfl | hub := eq_or_lt_of_le hub
    ¬∑ linarith [hd.2]
    obtain ‚ü®w, hwp, ‚ü®hwu, hwb‚ü©, hwf‚ü© :
        ‚àÉ w, d < dist (p a) (p w) ‚àß w ‚àà Icc u b ‚àß dist (f w) (f u) < delta - Œ¥ := by
      have : (ùìù[‚â•] u).NeBot := nhdsWithin_Ici_self_neBot u
      have H2 : ‚àÄ·∂† s in ùìù[‚â•] u, s ‚àà Icc u b := Icc_mem_nhdsWithin_Ici' hub
      have : Icc u b ‚äÜ Icc a b := Icc_subset_Icc_left hau
      have := H1.filter_mono (nhdsWithin_mono _ this)
      rw [nhdsWithin_Icc_eq_nhdsWithin_Ici hub] at this
      exact (H3 hub).and_eventually (H2.and this) |>.exists
    rw [dist_comm] at hwf
    have : dist (p u) (p w) ‚â§ dist (f u) (f w) + 4 * Œ¥ + 2 * C := by
      apply proj_along_quasiconvex_contraction' h (hfG _ ‚ü®hau, hub.le‚ü©) (hfG _ _)
      exact ‚ü®hau.trans hwu, hwb‚ü©
    have : dist (p a) (p w) ‚â§ dist (p a) (p u) + dist (p u) (p w) := dist_triangle ..
    linarith

-- FIXME decide whether this should be global
attribute [simp] le_neg neg_le

/-- Same lemma, except that one exchanges the roles of the beginning and the end point. -/
-- not sure whether inequalities are sharp or non-sharp
lemma quasi_convex_projection_small_gaps' {f p : ‚Ñù ‚Üí M} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    (hab : a ‚â§ b)
    (h : quasiconvex C G)
    (hfG : ‚àÄ t ‚àà Icc a b, p t ‚àà proj_set (f t) G)
    {delta : ‚Ñù} (hdelta : delta > Œ¥)
    {d : ‚Ñù} (hd : d ‚àà Icc (4 * delta + 2 * C) (dist (p a) (p b))) :
    ‚àÉ t ‚àà Icc a b, (dist (p b) (p t) ‚àà Icc (d - 4 * delta - 2 * C) d)
                    ‚àß (‚àÄ s ‚àà Icc t b, dist (p b) (p s) ‚â§ d) := by
  have hf_neg : ContinuousOn (fun t : ‚Ñù => f (- t)) (Icc (-b) (-a)) := by
    refine hf.comp continuousOn_neg ?_
    aesop (add norm unfold MapsTo)
  let q := fun t ‚Ü¶ p (-t)
  obtain ‚ü®t, htab, htq, htq'‚ü© :
      ‚àÉ t ‚àà Icc (-b) (-a), dist (q (-b)) (q t) ‚àà Icc (d - 4 * delta - 2 * C) d
                    ‚àß ‚àÄ s ‚àà Icc (-b) t, dist (q (-b)) (q s) ‚â§ d := by
    refine quasi_convex_projection_small_gaps hf_neg ?_ h ?_ hdelta ?_ <;>
    aesop (add norm [dist_comm])
  refine ‚ü®-t, ?_, ?_, ?_‚ü©
  ¬∑ aesop
  ¬∑ simpa [q] using htq
  ¬∑ intro s hs
    convert htq' (-s) _ using 2 <;>
    aesop
